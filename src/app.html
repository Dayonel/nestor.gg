<!DOCTYPE html>
<html lang="en">

<head>
	<meta charset="utf-8" />
	<link rel="preconnect" href="https://fonts.googleapis.com">
	<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
	<link
		href="https://fonts.googleapis.com/css2?family=Handjet:wght@700&family=Roboto:wght@900&family=Source+Sans+3:wght@700&display=swap"
		rel="stylesheet">
	<link rel="apple-touch-icon" sizes="180x180" href="/apple-touch-icon.png">
	<link rel="icon" type="image/png" sizes="32x32" href="/favicon-32x32.png">
	<link rel="icon" type="image/png" sizes="16x16" href="/favicon-16x16.png">
	<link rel="manifest" href="/site.webmanifest">
	<link rel="mask-icon" href="/safari-pinned-tab.svg" color="#5bbad5">
	<meta name="msapplication-TileColor" content="#ff27d5">
	<meta name="theme-color" content="#ff27d5">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0" />
	%sveltekit.head%
</head>

<body data-sveltekit-preload-data="hover">
	<!-- Particles -->
	<script type="x-shader/x-vertex" id="particles-vs">
		//
		// Description : Array and textureless GLSL 2D simplex noise function.
		//      Author : Ian McEwan, Ashima Arts.
		//  Maintainer : ijm
		//     Lastmod : 20110822 (ijm)
		//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
		//               Distributed under the MIT License. See LICENSE file.
		//               https://github.com/ashima/webgl-noise
		// 

		vec3 mod289(vec3 x) {
		return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec2 mod289(vec2 x) {
		return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec3 permute(vec3 x) {
		return mod289(((x*34.0)+1.0)*x);
		}

		float snoise(vec2 v)
		{
		const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
							0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
							-0.577350269189626,  // -1.0 + 2.0 * C.x
							0.024390243902439); // 1.0 / 41.0
		// First corner
		vec2 i  = floor(v + dot(v, C.yy) );
		vec2 x0 = v -   i + dot(i, C.xx);

		// Other corners
		vec2 i1;
		//i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
		//i1.y = 1.0 - i1.x;
		i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
		// x0 = x0 - 0.0 + 0.0 * C.xx ;
		// x1 = x0 - i1 + 1.0 * C.xx ;
		// x2 = x0 - 1.0 + 2.0 * C.xx ;
		vec4 x12 = x0.xyxy + C.xxzz;
		x12.xy -= i1;

		// Permutations
		i = mod289(i); // Avoid truncation effects in permutation
		vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
				+ i.x + vec3(0.0, i1.x, 1.0 ));

		vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
		m = m*m ;
		m = m*m ;

		// Gradients: 41 points uniformly over a line, mapped onto a diamond.
		// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

		vec3 x = 2.0 * fract(p * C.www) - 1.0;
		vec3 h = abs(x) - 0.5;
		vec3 ox = floor(x + 0.5);
		vec3 a0 = x - ox;

		// Normalise gradients implicitly by scaling m
		// Approximation of: m *= inversesqrt( a0*a0 + h*h );
		m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

		// Compute final noise value at P
		vec3 g;
		g.x  = a0.x  * x0.x  + h.x  * x0.y;
		g.yz = a0.yz * x12.xz + h.yz * x12.yw;
		return 130.0 * dot(m, g);
		}
		// @author brunoimbrizi / http://brunoimbrizi.com

		precision highp float;

		attribute float pindex;
		attribute vec3 position;
		attribute vec3 offset;
		attribute vec2 uv;
		attribute float angle;

		uniform mat4 modelViewMatrix;
		uniform mat4 projectionMatrix;

		uniform float uTime;
		uniform float uRandom;
		uniform float uDepth;
		uniform float uSize;
		uniform vec2 uTextureSize;
		uniform sampler2D uTexture;
		uniform sampler2D uTouch;

		varying vec2 vPUv;
		varying vec2 vUv;

		// #pragma glslify: snoise2 = require(glsl-noise/simplex/2d)

		float random(float n) {
			return fract(sin(n) * 43758.5453123);
		}

		void main() {
			vUv = uv;

			// particle uv
			vec2 puv = offset.xy / uTextureSize;
			vPUv = puv;

			// pixel color
			vec4 colA = texture2D(uTexture, puv);
			float grey = colA.r * 0.21 + colA.g * 0.71 + colA.b * 0.07;

			// displacement
			vec3 displaced = offset;
			// randomise
			displaced.xy += vec2(random(pindex) - 0.5, random(offset.x + pindex) - 0.5) * uRandom;
			float rndz = (random(pindex) + snoise(vec2(pindex * 0.1, uTime * 0.1)));
			displaced.z += rndz * (random(pindex) * 2.0 * uDepth);
			// center
			displaced.xy -= uTextureSize * 0.5;

			// touch
			float t = texture2D(uTouch, puv).r;
			displaced.z += t * 20.0 * rndz;
			displaced.x += cos(angle) * t * 20.0 * rndz;
			displaced.y += sin(angle) * t * 20.0 * rndz;

			// particle size
			float psize = (snoise(vec2(uTime, pindex) * 0.5) + 2.0);
			psize *= max(grey, 0.2);
			psize *= uSize;

			// final position
			vec4 mvPosition = modelViewMatrix * vec4(displaced, 1.0);
			mvPosition.xyz += position * psize;
			vec4 finalPosition = projectionMatrix * mvPosition;

			gl_Position = finalPosition;
		}
	</script>

	<script type="x-shader/x-fragment" id="particles-fs">
		// @author brunoimbrizi / http://brunoimbrizi.com

		precision highp float;

		uniform sampler2D uTexture;

		varying vec2 vPUv;
		varying vec2 vUv;

		void main() {
			vec4 color = vec4(0.0);
			vec2 uv = vUv;
			vec2 puv = vPUv;

			// pixel color
			vec4 colA = texture2D(uTexture, puv);

			// greyscale
			float grey = colA.r * 0.21 + colA.g * 0.71 + colA.b * 0.07;
			vec4 colB = vec4(grey, grey, grey, 1.0);

			// circle
			float border = 0.3;
			float radius = 0.5;
			float dist = radius - distance(uv, vec2(0.5));
			float t = smoothstep(0.0, border, dist);

			// final color
			color = colB;
			color.a = t;

			gl_FragColor = color;
		}
	</script>

	<!-- Particle animation ideas video by Yuri Artiukh 
		https://www.youtube.com/watch?v=oLH00MXTqNg
 	-->
	<script type="x-shader/x-vertex" id="morph-vs">
		uniform float time;
		uniform float progress;
		varying vec2 vUv;
		varying vec3 vPosition;
		uniform sampler2D texture;
		uniform float pointSize; // New parameter for point size
		float PI = 3.141592653589793238462643383279;

		//
		// Description : Array and textureless GLSL 2D simplex noise function.
		//      Author : Ian McEwan, Ashima Arts.
		//  Maintainer : ijm
		//     Lastmod : 20110822 (ijm)
		//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
		//               Distributed under the MIT License. See LICENSE file.
		//               https://github.com/ashima/webgl-noise
		//

		vec3 mod289(vec3 x) {
			return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec2 mod289(vec2 x) {
			return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec3 permute(vec3 x) {
			return mod289(((x * 34.0) + 1.0) * x);
		}

		float noise(vec2 v) {
			const vec4 C = vec4(0.211324865405187, // (3.0-sqrt(3.0))/6.0
					0.366025403784439, // 0.5*(sqrt(3.0)-1.0)
					-0.577350269189626, // -1.0 + 2.0 * C.x
					0.024390243902439); // 1.0 / 41.0
			// First corner
			vec2 i = floor(v + dot(v, C.yy));
			vec2 x0 = v - i + dot(i, C.xx);

			// Other corners
			vec2 i1;
			i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
			vec4 x12 = x0.xyxy + C.xxzz;
			x12.xy -= i1;

			// Permutations
			i = mod289(i); // Avoid truncation effects in permutation
			vec3 p = permute(permute(i.y + vec3(0.0, i1.y, 1.0))
					+ i.x + vec3(0.0, i1.x, 1.0));

			vec3 m = max(0.5 - vec3(dot(x0, x0), dot(x12.xy, x12.xy), dot(x12.zw, x12.zw)), 0.0);
			m = m * m;
			m = m * m;

			// Gradients: 41 points uniformly over a line, mapped onto a diamond.
			// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

			vec3 x = 2.0 * fract(p * C.www) - 1.0;
			vec3 h = abs(x) - 0.5;
			vec3 ox = floor(x + 0.5);
			vec3 a0 = x - ox;

			// Normalise gradients implicitly by scaling m
			// Approximation of: m *= inversesqrt( a0*a0 + h*h );
			m *= 1.79284291400159 - 0.85373472095314 * (a0 * a0 + h * h);

			// Compute final noise value at P
			vec3 g;
			g.x = a0.x * x0.x + h.x * x0.y;
			g.yz = a0.yz * x12.xz + h.yz * x12.yw;
			return 130.0 * dot(m, g);
		}

		vec3 curl(float x, float y, float z) {

			float eps = 1.,
			eps2 = 2. * eps;
			float n1,
			n2,
			a,
			b;

			x += time * .05;
			y += time * .05;
			z += time * .05;

			vec3 curl = vec3(0.);

			n1 = noise(vec2(x, y + eps));
			n2 = noise(vec2(x, y - eps));
			a = (n1 - n2) / eps2;

			n1 = noise(vec2(x, z + eps));
			n2 = noise(vec2(x, z - eps));
			b = (n1 - n2) / eps2;

			curl.x = a - b;

			n1 = noise(vec2(y, z + eps));
			n2 = noise(vec2(y, z - eps));
			a = (n1 - n2) / eps2;

			n1 = noise(vec2(x + eps, z));
			n2 = noise(vec2(x + eps, z));
			b = (n1 - n2) / eps2;

			curl.y = a - b;

			n1 = noise(vec2(x + eps, y));
			n2 = noise(vec2(x - eps, y));
			a = (n1 - n2) / eps2;

			n1 = noise(vec2(y + eps, z));
			n2 = noise(vec2(y - eps, z));
			b = (n1 - n2) / eps2;

			curl.z = a - b;

			return curl;
		}

		void main() {
			vUv = uv;

			vec3 t = position + curl(2. * position.x, 2. * position.y, 2. * position.z) * 10. * progress;
			float d = length(position - t) / 2.;

			// Adjust density based on progress
			float density = mix(10.0, 100.0, progress);

			// Use distanceDensity in the mix function
			vec3 np = mix(position, t, pow(d, 5.));

			vec4 mvPosition = modelViewMatrix * vec4(np, 1.);

			gl_PointSize = pointSize;
			gl_Position = projectionMatrix * mvPosition;
		}

	</script>

	<script type="x-shader/x-fragment" id="morph-fs">
		uniform float time;
		uniform vec4 resolution;
		varying vec2 vUv;
		varying vec3 vPosition;
		float PI = 3.141592653589793238462643383279;
		
		void main() {
			float alpha = 1. - smoothstep(-0.2, 0.5, length(gl_PointCoord - vec2(0.5)));
			alpha *= 0.5;
		
			gl_FragColor = vec4(vUv, 1.0, 1.0 * alpha);
		}		
	</script>

	<!-- Noise -->
	<script type="x-shader/x-vertex" id="noise-vs">
		varying vec2 vUv;

		void main() {
			vUv = uv;
			gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
		}
	</script>

	<script type="x-shader/x-fragment" id="noise-fs">
		// Description : Array and textureless GLSL 2D/3D/4D simplex
		//               noise functions.
		//      Author : Ian McEwan, Ashima Arts.
		//  Maintainer : stegu
		//     Lastmod : 20110822 (ijm)
		//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
		//               Distributed under the MIT License. See LICENSE file.
		//               https://github.com/ashima/webgl-noise
		//               https://github.com/stegu/webgl-noise
		//

		vec3 mod289(vec3 x) {
			return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 mod289(vec4 x) {
			return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 permute(vec4 x) {
			return mod289(((x * 34.0) + 1.0) * x);
		}

		vec4 taylorInvSqrt(vec4 r) {
			return 1.79284291400159 - 0.85373472095314 * r;
		}

		float snoise(vec3 v) {
			const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
			const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

			// First corner
			vec3 i = floor(v + dot(v, C.yyy));
			vec3 x0 = v - i + dot(i, C.xxx);

			// Other corners
			vec3 g = step(x0.yzx, x0.xyz);
			vec3 l = 1.0 - g;
			vec3 i1 = min(g.xyz, l.zxy);
			vec3 i2 = max(g.xyz, l.zxy);

			//   x0 = x0 - 0.0 + 0.0 * C.xxx;
			//   x1 = x0 - i1  + 1.0 * C.xxx;
			//   x2 = x0 - i2  + 2.0 * C.xxx;
			//   x3 = x0 - 1.0 + 3.0 * C.xxx;
			vec3 x1 = x0 - i1 + C.xxx;
			vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
			vec3 x3 = x0 - D.yyy; // -1.0+3.0*C.x = -0.5 = -D.y

			// Permutations
			i = mod289(i);
			vec4 p = permute(permute(permute(
							i.z + vec4(0.0, i1.z, i2.z, 1.0))
						+ i.y + vec4(0.0, i1.y, i2.y, 1.0))
					+ i.x + vec4(0.0, i1.x, i2.x, 1.0));

			// Gradients: 7x7 points over a square, mapped onto an octahedron.
			// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
			float n_ = 0.142857142857; // 1.0/7.0
			vec3 ns = n_ * D.wyz - D.xzx;

			vec4 j = p - 49.0 * floor(p * ns.z * ns.z); //  mod(p,7*7)

			vec4 x_ = floor(j * ns.z);
			vec4 y_ = floor(j - 7.0 * x_); // mod(j,N)

			vec4 x = x_ * ns.x + ns.yyyy;
			vec4 y = y_ * ns.x + ns.yyyy;
			vec4 h = 1.0 - abs(x) - abs(y);

			vec4 b0 = vec4(x.xy, y.xy);
			vec4 b1 = vec4(x.zw, y.zw);

			//vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
			//vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
			vec4 s0 = floor(b0) * 2.0 + 1.0;
			vec4 s1 = floor(b1) * 2.0 + 1.0;
			vec4 sh = -step(h, vec4(0.0));

			vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
			vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

			vec3 p0 = vec3(a0.xy, h.x);
			vec3 p1 = vec3(a0.zw, h.y);
			vec3 p2 = vec3(a1.xy, h.z);
			vec3 p3 = vec3(a1.zw, h.w);

			//Normalise gradients
			vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
			p0 *= norm.x;
			p1 *= norm.y;
			p2 *= norm.z;
			p3 *= norm.w;

			// Mix final noise value
			vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
			m = m * m;
			return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1),
					dot(p2, x2), dot(p3, x3)));
		}

		uniform float time;
		uniform vec3 diffuse;
		uniform float opacity;
    	varying vec2 vUv;

		void main() {
			vec2 tUv = vUv * 5.0;
			tUv -= vec2(time * 0.5, time);
			float n1 = snoise(vec3(tUv, time * 0.125));
			n1 = (n1 + 1.0) * 0.5;
			
			float n2 = snoise(vec3(n1 * 10., 1, 1));
			n2 = sin(((n2 + 1.0) * 0.5) * 3.1415926 * 2.);
			
			float effect = smoothstep(0.1, 0.125, n1) * (1. - smoothstep(0.375, 0.4, n2));
			float coef = sin(n2 * 3.141526 * 0.5) * 0.125;
			float e = effect - abs(coef);
			e = n1 > 0.25 && n1 < 0.75? e * e : pow(e, 16.);

			vec4 finalColor = vec4(diffuse * e, opacity);
        	gl_FragColor = finalColor;
		}
	</script>

	<div style="display: contents">%sveltekit.body%</div>
</body>

</html>